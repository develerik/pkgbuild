# Maintainer: Erik Bender <erik.bender@develerik.dev>

pkgname=osv-scanner-git
pkgver=1.0.1.r0.gac28fb9
pkgrel=1
pkgdesc="Vulnerability scanner written in Go which uses the data provided by https://osv.dev"
url="https://github.com/google/osv-scanner"
arch=('x86_64' 'i686')
license=('Apache License 2.0')
provides=(osv-scanner)
makedepends=('go')
source=("${pkgname}::git+${url}")
sha256sums=('SKIP')

pkgver() {
	cd "${srcdir}/${pkgname}"

	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//g'
}

prepare() {
	cd "${srcdir}/${pkgname}"

	install -m755 -d "${srcdir}/go/src/github.com/google/"
	ln -sf "${srcdir}/${pkgname}" "${srcdir}/go/src/github.com/google/osv-scanner"

	cd "${srcdir}/go/src/github.com/google/osv-scanner/cmd/osv-scanner"

  export GOPATH="${srcdir}/go" GOFLAGS="-modcacherw"
  go get -v -t ./...
}

build() {
	cd "${srcdir}/go/src/github.com/google/osv-scanner/cmd/osv-scanner"

	mkdir -p build

  export GOPATH="${srcdir}/go" GOFLAGS="-modcacherw"
	go build \
		-trimpath -ldflags "-s -w" \
		-o ./build/osv-scanner .
}

package() {
	cd "${srcdir}/go/src/github.com/google/osv-scanner"

	install -Dm755 "cmd/osv-scanner/build/osv-scanner" "${pkgdir}/usr/bin/osv-scanner"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
